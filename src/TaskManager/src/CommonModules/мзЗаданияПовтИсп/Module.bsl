
#Область ПрограммныйИнтерфейс

// Возвращает структуру с описанием параметров метода.
// 
// Параметры:
//  Метод СправочникСсылка.Метод
// 
// Возвращаемое значение:
//  Структура - Описание параметров метода
//	 Ключ - Имя параметра
//	 Значение - Описание типов
Функция ОписаниеПараметровМетода(Метод) Экспорт

	ПараметрыМетода = Новый Структура;

	Если Не ЗначениеЗаполнено(Метод) Тогда
		Возврат ПараметрыМетода;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	мзМетодыПараметрыМетода.ИмяПараметра,
	|	мзМетодыПараметрыМетода.ТипПараметра,
	|	мзМетодыПараметрыМетода.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	Справочник.мзМетоды.ПараметрыМетода КАК мзМетодыПараметрыМетода
	|ГДЕ
	|	мзМетодыПараметрыМетода.Ссылка = &Ссылка
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	Запрос.УстановитьПараметр("Ссылка", Метод);

	Выборка = Запрос.Выполнить().Выбрать();

	Пока Выборка.Следующий() Цикл
		ПараметрыМетода.Вставить(Выборка.ИмяПараметра, Новый ОписаниеТипов(Выборка.ТипПараметра));
	КонецЦикла;

	Возврат ПараметрыМетода;
	
КонецФункции

#КонецОбласти